dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

publishing {
    publications {
        Production(MavenPublication) {
            artifactId = project.ext.artifactId

            pom {
                name = project.ext.name
                description = project.ext.description
                url = project.ext.url
                scm {
                    connection = 'scm:git@github.com/ActiveLook/sdk-android.git'
                    developerConnection = 'scm:git@github.com/ActiveLook/sdk-android.git'
                    url = 'https://github.com/ActiveLook/sdk-android'
                }

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'activelook'
                        name = 'ActiveLook'
                    }
                }
            }
            artifact("$buildDir/outputs/aar/sdk-${project.version}-release.aar")  {
                builtBy tasks.getByName("assemble")
            }



            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}